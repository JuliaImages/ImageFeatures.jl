using Images

"""
```
lines = hough_transform_standard(image, ρ, θ, threshold, linesMax)
```

Returns a vector of tuples corresponding to the tuples of (r,t)
where r and t are parameters for normal form of line:
    `x \* cos(t) + y \* sin(t) = r`

-   `r` = length of perpendicular from (1,1) to the line
-   `t` = angle between perpendicular from (1,1) to the line and x-axis

The lines are generated by applying hough transform on the image.

Parameters:
-    `image`       = Image to be transformed (eltype should be `Bool`)
-    `ρ`           = Discrete step size for perpendicular length of line
-    `θ`           = List of angles for which the transform is computed
-    `threshold`   = Accumulator threshold for line detection
-    `linesMax`    = Maximum no of lines to return

# Example
```julia
julia> img = load("line.jpg");

julia> img_edges = canny(img, (Percentile(0.99), Percentile(0.97)), 1);

julia> lines = hough_transform_standard(img_edges, 1, linspace(0,π,30), 40, 5)
5-element Array{Tuple{Float64,Float64},1}:
 (45.0,1.73329)  
 (1.0,1.73329)   
 (32.0,1.73329)  
 (209.0,0.649985)
 (-9.0,2.49161) 
```
"""

function hough_transform_standard(
img::AbstractArray{T,2},
ρ::Real, θ::Range,
threshold::Integer, linesMax::Integer) where T<:Union{Bool,Gray{Bool}}


    #function to compute local maximum lines with values > threshold and return a vector containing them
    function findlocalmaxima!(validLines::AbstractVector{CartesianIndex{2}}, accumulator_matrix::Array{Int,2}, threshold::T) where T<:Integer
        for val in CartesianRange(size(accumulator_matrix))
            if  accumulator_matrix[val] >  threshold                             &&
                accumulator_matrix[val] >  accumulator_matrix[val[1],val[2] - 1] &&
                accumulator_matrix[val] >= accumulator_matrix[val[1],val[2] + 1] &&
                accumulator_matrix[val] >  accumulator_matrix[val[1] - 1,val[2]] &&
                accumulator_matrix[val] >= accumulator_matrix[val[1] + 1,val[2]]
                push!(validLines,val)
            end
        end
    end

    ρ > 0 || error("Discrete step size must be positive")

    indsy, indsx = indices(img)
    ρinv = 1 / ρ
    numangle = length(θ)
    numrho = round(Int,(2(length(indsx) + length(indsy)) + 1)*ρinv)

    accumulator_matrix = zeros(Int, numangle + 2, numrho + 2)

    #Pre-Computed sines and cosines in tables
    sinθ, cosθ = sin.(θ).*ρinv, cos.(θ).*ρinv

    #Hough Transform implementation
    constadd = round(Int,(numrho -1)/2)
    for pix in CartesianRange(size(img))
        if img[pix]
            for i in 1:numangle
                dist = round(Int, pix[1] * sinθ[i] + pix[2] * cosθ[i])
                dist += constadd
                accumulator_matrix[i + 1, dist + 1] += 1
            end
        end
    end

    #Finding local maximum lines
    validLines = Vector{CartesianIndex{2}}(0)
    findlocalmaxima!(validLines, accumulator_matrix, threshold)

    #Sorting by value in accumulator_matrix
    @noinline sort_by_votes(validLines, accumulator_matrix) = sort!(validLines, lt = (a,b)-> accumulator_matrix[a]>accumulator_matrix[b])
    sort_by_votes(validLines, accumulator_matrix)

    linesMax = min(linesMax, length(validLines))

    lines = Vector{Tuple{Float64,Float64}}(0)

    #Getting lines with Maximum value in accumulator_matrix && size(lines) < linesMax
    for l in 1:linesMax
        lrho = ((validLines[l][2]-1) - (numrho-1)*0.5)*ρ
        langle = θ[validLines[l][1]-1]
        push!(lines,(lrho,langle))
    end

    lines

end

"""
```
circle_centers, circle_radius = hough_circle_gradient(img_edges, img_phase, scale, min_dist, vote_thres, min_radius:max_radius)  
```
Returns two vectors, corresponding to circle centers and radius.  
  
The circles are generated using a hough transform variant in which a non-zero point only votes for circle  
centers perpendicular to the local gradient. In case of concentric circles, only the largest circle is detected.
  
Parameters:  
-   `img_edges`    = edges of the image  
-   `img_phase`    = phase of the gradient image   
-   `scale`        = relative accumulator resolution factor  
-   `min_dist`     = minimum distance between detected circle centers  
-   `vote_thres`   = accumulator threshold for circle detection  
-   `min_radius:max_radius`   = circle radius range

[`canny`](@ref) and [`phase`](@ref) can be used for obtaining img_edges and img_phase respectively.

# Example
```julia
img = load("circle.png")

img_edges = canny(img, 1, 0.99, 0.97)
dx, dy=imgradients(img, KernelFactors.ando5)
img_phase = phase(dx, dy)

centers, radii=hough_circle_gradient(img_edges, img_phase, 1, 60, 60, 3:50)
```
"""  

function hough_circle_gradient(
        img_edges::AbstractArray{Bool,2}, img_phase::AbstractArray{T,2},
        scale::Number, min_dist::Number,
        vote_thres::Number, radii::AbstractVector{Int}) where T<:Number

    rows,cols=size(img_edges)

    non_zeros=CartesianIndex{2}[]
    centers=CartesianIndex{2}[]
    circle_centers=CartesianIndex{2}[]
    circle_radius=Int[]
    accumulator_matrix=zeros(Int, Int(floor(rows/scale))+1, Int(floor(cols/scale))+1)

    function vote!(accumulator_matrix, x, y)
        fx = Int(floor(x))
        fy = Int(floor(y))

        for i in fx:fx+1
            for j in fy:fy+1
                if checkbounds(Bool, accumulator_matrix, i, j)
                    @inbounds accumulator_matrix[i, j] += 1
                end
            end
        end
    end

    for j in indices(img_edges, 2)
        for i in indices(img_edges, 1)
            if img_edges[i,j]
                sinθ = -cos(img_phase[i,j]);
                cosθ = sin(img_phase[i,j]);

                for r in radii
                    x=(i+r*sinθ)/scale
                    y=(j+r*cosθ)/scale
                    vote!(accumulator_matrix, x, y)

                    x=(i-r*sinθ)/scale
                    y=(j-r*cosθ)/scale
                    vote!(accumulator_matrix, x, y)
                end
                push!(non_zeros, CartesianIndex{2}(i,j));
            end
        end
    end

    for i in findlocalmaxima(accumulator_matrix)
        if accumulator_matrix[i]>vote_thres
            push!(centers, i);
        end
    end

    @noinline sort_by_votes(centers, accumulator_matrix) = sort!(centers, lt=(a, b) -> accumulator_matrix[a]>accumulator_matrix[b])

    sort_by_votes(centers, accumulator_matrix)

    dist(a, b) = sqrt(sum(abs2, (a-b).I))

    f = CartesianIndex(map(r->first(r), indices(accumulator_matrix)))
    l = CartesianIndex(map(r->last(r), indices(accumulator_matrix)))
    radius_accumulator=Vector{Int}(Int(floor(dist(f,l)/scale)+1))

    for center in centers
        center=(center-1)*scale
        fill!(radius_accumulator, 0)

        too_close=false
        for circle_center in circle_centers
            if dist(center, circle_center)< min_dist
                too_close=true
                break
            end
        end
        if too_close
            continue;
        end

        for point in non_zeros
            r=Int(floor(dist(center, point)/scale))
            if radii.start/scale<=r<=radii.stop/scale
                radius_accumulator[r+1]+=1
            end
        end

        voters, radius = findmax(radius_accumulator)
        radius=(radius-1)*scale;

        if voters>vote_thres
            push!(circle_centers, center)
            push!(circle_radius, radius)
        end
    end
    return circle_centers, circle_radius
end

